# Copyright (c) 2015 The Native Client Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/toolchain/toolchain.gni")

# This is a special toolchain used solely for building GNU/AT&T-syntax
# assembly files on Windows.  See src/trusted/asm.gni for more details.

toolchain("nacl_win_as") {
  tool("asm") {
    # For GCC we can just use the C compiler to compile assembly.
    win_as_script = rebase_path("//native_client/tools/win_as.py",
                                root_build_dir)
    if (current_cpu == "x86") {
      win_as_arch = "Win32"
    } else {
      win_as_arch = current_cpu
    }
    nacl_path = rebase_path("//native_client", root_build_dir)
    command = "$python_path $win_as_script -a $win_as_arch -p $nacl_path -o {{output}} {{source}}"
    depsformat = "msvc"
    description = "GNU ASM {{output}}"
    outputs = [
      "{{target_out_dir}}/{{target_output_name}}/{{source_name_part}}.obj",
    ]
  }

  # These two are really entirely generic, but have to be repeated in
  # each toolchain because GN doesn't allow a template to be used here.
  # See //build/toolchain/toolchain.gni for details.
  tool("stamp") {
    command = stamp_command
    description = stamp_description
  }
  tool("copy") {
    command = copy_command
    description = copy_description
  }
}

# Copyright (c) 2014 The Native Client Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//native_client/build/config/sysroot.gni")
import("//native_client/build/toolchain/gcc_toolchain.gni")
import("//native_client/build/toolchain/nacl_toolchain.gni")

nacl_toolchain("newlib_arm") {
  package = "nacl_arm_newlib"
  toolchain_cpu_arch = "arm"
  toolprefix = "toolchain/${os}_x86/${package}/bin/arm-nacl-"

  cc = toolprefix + "gcc"
  cxx = toolprefix + "g++"
  ar = toolprefix + "ar"
  ld = cxx
}

nacl_toolchain("newlib_x86") {
  package = "nacl_x86_newlib"
  toolchain_cpu_arch = "x86"
  toolprefix = "toolchain/${os}_x86/${package}/bin/i686-nacl-"

  cc = toolprefix + "gcc"
  cxx = toolprefix + "g++"
  ar = toolprefix + "ar"
  ld = cxx
}

nacl_toolchain("newlib_x64") {
  package = "nacl_x86_newlib"
  toolchain_cpu_arch = "x64"
  toolprefix = "toolchain/${os}_x86/${package}/bin/x86_64-nacl-"

  cc = toolprefix + "gcc"
  cxx = toolprefix + "g++"
  ar = toolprefix + "ar"
  ld = cxx
}

nacl_toolchain("glibc_x86") {
  package = "nacl_x86_glibc"
  toolchain_cpu_arch = "x86"
  toolprefix = "toolchain/${os}_x86/${package}/bin/i686-nacl-"

  cc = toolprefix + "gcc"
  cxx = toolprefix + "g++"
  ar = toolprefix + "ar"
  ld = cxx
}

nacl_toolchain("glibc_x64") {
  package = "nacl_x86_glibc"
  toolchain_cpu_arch = "x64"
  toolprefix = "toolchain/${os}_x86/${package}/bin/x86_64-nacl-"

  cc = toolprefix + "gcc"
  cxx = toolprefix + "g++"
  ar = toolprefix + "ar"
  ld = cxx
}

nacl_toolchain("clang_newlib_x86") {
  package = "pnacl_newlib"
  toolchain_cpu_arch = "x86"
  toolprefix = "toolchain/${os}_x86/${package}/bin/i686-nacl-"
  is_clang = true

  cc = toolprefix + "clang"
  cxx = toolprefix + "clang++"
  ar = toolprefix + "ar"
  ld = cxx
}

nacl_toolchain("clang_newlib_x64") {
  package = "pnacl_newlib"
  toolchain_cpu_arch = "x64"
  toolprefix = "toolchain/${os}_x86/${package}/bin/x86_64-nacl-"
  is_clang = true

  cc = toolprefix + "clang"
  cxx = toolprefix + "clang++"
  ar = toolprefix + "ar"
  ld = cxx
}

link_irt = rebase_path("//native_client/build/link_irt.py", root_build_dir)

nacl_toolchain("irt_x86") {
  package = "pnacl_newlib"
  toolchain_cpu_arch = "x86"
  toolprefix = "toolchain/${os}_x86/${package}/bin/i686-nacl-"
  is_clang = true

  cc = toolprefix + "clang"
  cxx = toolprefix + "clang++"
  ar = toolprefix + "ar"
  readelf = toolprefix + "readelf"
  # Link with cc to avoid including any C++ libs.
  ld = "${python_path} ${link_irt} --tls-edit=tls_edit --link-cmd=${cc} --readelf-cmd=${readelf}"

  # TODO(ncbray): depend on link script
  deps = [
    "//native_client/src/tools/tls_edit:tls_edit($host_toolchain)",
  ]
}

nacl_toolchain("irt_x64") {
  package = "pnacl_newlib"
  toolchain_cpu_arch = "x64"
  toolprefix = "toolchain/${os}_x86/${package}/bin/x86_64-nacl-"
  is_clang = true

  cc = toolprefix + "clang"
  cxx = toolprefix + "clang++"
  ar = toolprefix + "ar"
  readelf = toolprefix + "readelf"
  # Link with cc to avoid including any C++ libs.
  ld = "${python_path} ${link_irt} --tls-edit=tls_edit --link-cmd=${cc} --readelf-cmd=${readelf}"

  # TODO(ncbray): depend on link script
  deps = [
    "//native_client/src/tools/tls_edit:tls_edit($host_toolchain)",
  ]
}

# Uses newlib to match the Chrome build.
nacl_toolchain("irt_arm") {
  package = "nacl_arm_newlib"
  toolchain_cpu_arch = "arm"
  toolprefix = "toolchain/${os}_x86/${package}/bin/arm-nacl-"

  cc = toolprefix + "gcc"
  cxx = toolprefix + "g++"
  ar = toolprefix + "ar"
  readelf = toolprefix + "readelf"
  # Link with cc to avoid including any C++ libs.
  ld = "${python_path} ${link_irt} --tls-edit=tls_edit --link-cmd=${cc} --readelf-cmd=${readelf}"

  # TODO(ncbray): depend on link script
  deps = [
    "//native_client/src/tools/tls_edit:tls_edit($host_toolchain)",
  ]
}

# A fake toolchain to namespace the toolchain prep.
# Doing toolchain prep inside a non-default toolchain prevents it from becoming
# a default target, which means that it will only trigger if explicitly depended
# upon.
gcc_toolchain("toolchain_prep") {
  cc = "clang"
  cxx = "clang++"
  ar = "ar"
  ld = cxx
  toolchain_cpu_arch = cpu_arch
  toolchain_os = os
}
